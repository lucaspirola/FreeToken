// Adatped from
// https://github.com/vllm-project/vllm/blob/755ed7b05be4743237d3339c4ff8c22bcaae04f4/csrc/quantization/gguf/gguf_kernel.cu
#include <c10/cuda/CUDAGuard.h>
#include <cuda_fp16.h>
#include <cuda_runtime.h>
#include <torch/all.h>

// dont use clang-format here, it breaks the include order
// clang-format off
#include "dispatch.h"

#include "ggml-common.h"
#include "vecdotq.cuh"
#include "dequantize.cuh"
#include "mmvq.cuh"
#include "mmq.cuh"
#include "moe.cuh"
#include "moe_vec.cuh"
// clang-format off

// Q8 gemv
template <typename scalar_t>
static __global__ void
quantize_q8_1(const scalar_t* __restrict__ x, void* __restrict__ vy, const int kx, const int kx_padded) {
  const auto ix = blockDim.x * blockIdx.x + threadIdx.x;
  if (ix >= kx_padded) {
    return;
  }
  const auto iy = blockDim.y * blockIdx.y + threadIdx.y;
  const int i_padded = iy * kx_padded + ix;

  block_q8_1* y = (block_q8_1*)vy;

  const int ib = i_padded / QK8_1;   // block index
  const int iqs = i_padded % QK8_1;  // quant index

  const float xi = ix < kx ? static_cast<float>(x[iy * kx + ix]) : 0.0f;
  float amax = fabsf(xi);
  float sum = xi;

#pragma unroll
  for (int mask = 16; mask > 0; mask >>= 1) {
    amax = fmaxf(amax, SGLANG_SHFL_XOR_SYNC_WIDTH(uint32_t(-1), amax, mask, 32));
    sum += SGLANG_SHFL_XOR_SYNC_WIDTH(uint32_t(-1), sum, mask, 32);
  }

  const float d = amax / 127;
  const int8_t q = amax == 0.0f ? 0 : roundf(xi / d);

  y[ib].qs[iqs] = q;

  if (iqs > 0) {
    return;
  }

  y[ib].ds.x = __float2half(d);
  y[ib].ds.y = __float2half(sum);
}

template <typename scalar_t>
static void quantize_row_q8_1_cuda(const scalar_t* x, void* vy, const int kx, const int ky, cudaStream_t stream) {
  const int64_t kx_padded = (kx + 512 - 1) / 512 * 512;
  const int block_num_x = (kx_padded + CUDA_QUANTIZE_BLOCK_SIZE - 1) / CUDA_QUANTIZE_BLOCK_SIZE;
  constexpr int MAX_BLOCK_SIZE = 65535;
  for (int off = 0; off < ky; off += MAX_BLOCK_SIZE) {
    const int num_blocks_y = std::min(ky, off + MAX_BLOCK_SIZE) - off;
    const dim3 num_blocks(block_num_x, num_blocks_y, 1);
    const dim3 block_size(CUDA_DEQUANTIZE_BLOCK_SIZE, 1, 1);
    quantize_q8_1<<<num_blocks, block_size, 0, stream>>>(
        &x[off * kx], (int32_t*)vy + off * (kx_padded / 32 * 9), kx, kx_padded);
  }
}

template <typename scalar_t>
static __global__ void quantize_permuted_v_q8_1(
    const scalar_t* __restrict__ x,
    void* __restrict__ vy,
    const int kx,
    const int kx_padded,
    const int num_key_heads,
    const int values_per_key,
    const int head_dim) {
  const int ix = blockDim.x * blockIdx.x + threadIdx.x;
  if (ix >= kx_padded) return;
  const int iy = blockDim.y * blockIdx.y + threadIdx.y;
  const int i_padded = iy * kx_padded + ix;
  block_q8_1* y = (block_q8_1*)vy;
  const int ib = i_padded / QK8_1;
  const int iqs = i_padded % QK8_1;

  float value = 0.0f;
  if (ix < kx) {
    // Destination is llama.cpp tiled order [values_per_key, key_head, dim].
    // Source FLA output is grouped order [key_head, values_per_key, dim].
    const int dim = ix % head_dim;
    const int head = ix / head_dim;
    const int key_head = head % num_key_heads;
    const int value_in_key = head / num_key_heads;
    const int src = (key_head * values_per_key + value_in_key) * head_dim + dim;
    value = static_cast<float>(x[(int64_t)iy * kx + src]);
  }
  float amax = fabsf(value);
  float sum = value;
#pragma unroll
  for (int mask = 16; mask > 0; mask >>= 1) {
    amax = fmaxf(amax, SGLANG_SHFL_XOR_SYNC_WIDTH(uint32_t(-1), amax, mask, 32));
    sum += SGLANG_SHFL_XOR_SYNC_WIDTH(uint32_t(-1), sum, mask, 32);
  }
  const float d = amax / 127;
  y[ib].qs[iqs] = amax == 0.0f ? 0 : roundf(value / d);
  if (iqs == 0) {
    y[ib].ds.x = __float2half(d);
    y[ib].ds.y = __float2half(sum);
  }
}

template <typename scalar_t>
static void quantize_permuted_v_q8_1_cuda(
    const scalar_t* x, void* vy, const int kx, const int rows,
    const int num_key_heads, const int values_per_key, const int head_dim,
    cudaStream_t stream) {
  const int padded = (kx + 512 - 1) / 512 * 512;
  const int blocks_x = (padded + CUDA_QUANTIZE_BLOCK_SIZE - 1) / CUDA_QUANTIZE_BLOCK_SIZE;
  const dim3 blocks(blocks_x, rows, 1);
  const dim3 threads(CUDA_DEQUANTIZE_BLOCK_SIZE, 1, 1);
  quantize_permuted_v_q8_1<<<blocks, threads, 0, stream>>>(
      x, vy, kx, padded, num_key_heads, values_per_key, head_dim);
}

template <typename scalar_t>
static __global__ void quantize_gdn_norm_permuted_v_q8_1(
    const scalar_t* __restrict__ x,
    const scalar_t* __restrict__ z,
    const scalar_t* __restrict__ norm_weight,
    void* __restrict__ vy,
    const int rows,
    const int num_key_heads,
    const int values_per_key,
    const int head_dim,
    const float eps) {
  const int ix = blockDim.x * blockIdx.x + threadIdx.x;
  const int kx = num_key_heads * values_per_key * head_dim;
  if (ix >= kx) return;
  const int iy = blockIdx.y;
  if (iy >= rows) return;
  const int lane = threadIdx.x & 31;

  const int dim = ix % head_dim;
  const int tiled_head = ix / head_dim;
  const int key_head = tiled_head % num_key_heads;
  const int value_in_key = tiled_head / num_key_heads;
  const int grouped_head = key_head * values_per_key + value_in_key;
  const int num_value_heads = num_key_heads * values_per_key;
  const int64_t src_base = ((int64_t)iy * num_value_heads + grouped_head) * head_dim;

  float square_sum = 0.0f;
  for (int d = lane; d < head_dim; d += 32) {
    const float xv = static_cast<float>(x[src_base + d]);
    square_sum += xv * xv;
  }
#pragma unroll
  for (int mask = 16; mask > 0; mask >>= 1) {
    square_sum += SGLANG_SHFL_XOR_SYNC_WIDTH(uint32_t(-1), square_sum, mask, 32);
  }
  const float rstd = rsqrtf(square_sum / head_dim + eps);
  const float xv = static_cast<float>(x[src_base + dim]);
  const float zv = static_cast<float>(z[src_base + dim]);
  const float w = static_cast<float>(norm_weight[dim]);
  // Match Triton's tl.sigmoid lowering used by rms_norm_gated.
  const float gate = zv / (1.0f + exp2f(-zv * 1.4426950408889634f));
  // Match the materialized BF16 gated-norm output before Q8_1 quantization.
  const float value = static_cast<float>(static_cast<scalar_t>(xv * rstd * w * gate));

  const int i_padded = iy * kx + ix;
  block_q8_1* y = (block_q8_1*)vy;
  const int ib = i_padded / QK8_1;
  const int iqs = i_padded % QK8_1;
  float amax = fabsf(value);
  float sum = value;
#pragma unroll
  for (int mask = 16; mask > 0; mask >>= 1) {
    amax = fmaxf(amax, SGLANG_SHFL_XOR_SYNC_WIDTH(uint32_t(-1), amax, mask, 32));
    sum += SGLANG_SHFL_XOR_SYNC_WIDTH(uint32_t(-1), sum, mask, 32);
  }
  const float d = amax / 127;
  y[ib].qs[iqs] = amax == 0.0f ? 0 : roundf(value / d);
  if (iqs == 0) {
    y[ib].ds.x = __float2half(d);
    y[ib].ds.y = __float2half(sum);
  }
}

template <typename scalar_t>
static __global__ void quantize_silu_row_q8_1(
    const scalar_t* __restrict__ gate_up,
    void* __restrict__ vy,
    const int kx,
    const int kx_padded) {
  const auto ix = blockDim.x * blockIdx.x + threadIdx.x;
  if (ix >= kx_padded) return;
  const auto iy = blockDim.y * blockIdx.y + threadIdx.y;
  const int i_padded = iy * kx_padded + ix;
  block_q8_1* y = (block_q8_1*)vy;
  const int ib = i_padded / QK8_1;
  const int iqs = i_padded % QK8_1;

  float value = 0.0f;
  if (ix < kx) {
    const int64_t row = (int64_t)iy * 2 * kx;
    const float gate = static_cast<float>(gate_up[row + ix]);
    const float up = static_cast<float>(gate_up[row + kx + ix]);
    // Match the existing Triton SiLU epilogue (ex2.approx), then round the
    // materialized activation to the model dtype before Q8_1 quantization.
    // The fused path must not accidentally gain an extra FP32 activation stage.
    const float activated = (gate / (1.0f + exp2f(-gate * 1.4426950408889634f))) * up;
    value = static_cast<float>(static_cast<scalar_t>(activated));
  }
  float amax = fabsf(value);
  float sum = value;
#pragma unroll
  for (int mask = 16; mask > 0; mask >>= 1) {
    amax = fmaxf(amax, SGLANG_SHFL_XOR_SYNC_WIDTH(uint32_t(-1), amax, mask, 32));
    sum += SGLANG_SHFL_XOR_SYNC_WIDTH(uint32_t(-1), sum, mask, 32);
  }
  const float d = amax / 127;
  y[ib].qs[iqs] = amax == 0.0f ? 0 : roundf(value / d);
  if (iqs == 0) {
    y[ib].ds.x = __float2half(d);
    y[ib].ds.y = __float2half(sum);
  }
}

template <typename scalar_t>
static void quantize_silu_row_q8_1_cuda(
    const scalar_t* gate_up, void* vy, const int kx, const int rows,
    cudaStream_t stream) {
  const int64_t padded = (kx + 512 - 1) / 512 * 512;
  const int blocks_x = (padded + CUDA_QUANTIZE_BLOCK_SIZE - 1) / CUDA_QUANTIZE_BLOCK_SIZE;
  const dim3 blocks(blocks_x, rows, 1);
  const dim3 threads(CUDA_DEQUANTIZE_BLOCK_SIZE, 1, 1);
  quantize_silu_row_q8_1<<<blocks, threads, 0, stream>>>(gate_up, vy, kx, padded);
}

torch::Tensor ggml_dequantize(
    torch::Tensor W,  // quant weight
    int64_t type,
    int64_t m,
    int64_t n,
    std::optional<at::ScalarType> const& dtype) {
  const at::cuda::OptionalCUDAGuard device_guard(device_of(W));
  auto dtype_ = dtype.value_or(torch::kFloat16);
  auto options = torch::TensorOptions().dtype(dtype_).device(W.device());
  at::Tensor DW = torch::empty({m, n}, options);
  cudaStream_t stream = at::cuda::getCurrentCUDAStream().stream();

  DISPATCH_FLOAT_TYPES(DW.scalar_type(), "ggml_dequantize", [&] {
    auto to_cuda = ggml_get_to_cuda<scalar_t>(type);
    to_cuda((void*)W.data_ptr(), (scalar_t*)DW.data_ptr(), m * n, stream);
  });

  return DW;
}

torch::Tensor ggml_mul_mat_vec_a8(
    torch::Tensor W,  // quant weight
    torch::Tensor X,  // input
    int64_t type,
    int64_t row) {
  int col = X.sizes()[1];
  int vecs = X.sizes()[0];
  const int padded = (col + 512 - 1) / 512 * 512;
  const at::cuda::OptionalCUDAGuard device_guard(device_of(X));
  auto options = torch::TensorOptions().dtype(X.dtype()).device(W.device());
  at::Tensor Y = torch::empty({vecs, row}, options);
  cudaStream_t stream = at::cuda::getCurrentCUDAStream().stream();
  options = torch::TensorOptions().dtype(torch::kInt32).device(W.device());
  at::Tensor quant_X = torch::empty({vecs, padded / 32 * 9}, options);
  DISPATCH_FLOAT_TYPES(X.scalar_type(), "ggml_mul_mat_vec_a8", [&] {
    quantize_row_q8_1_cuda<scalar_t>((scalar_t*)X.data_ptr(), (void*)quant_X.data_ptr(), col, vecs, stream);
    switch (type) {
      case 2:
        mul_mat_vec_q4_0_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
      case 3:
        mul_mat_vec_q4_1_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
      case 6:
        mul_mat_vec_q5_0_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
      case 7:
        mul_mat_vec_q5_1_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
      case 8:
        mul_mat_vec_q8_0_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
      case 10:
        mul_mat_vec_q2_K_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
      case 11:
        mul_mat_vec_q3_K_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
      case 12:
        mul_mat_vec_q4_K_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
      case 13:
        mul_mat_vec_q5_K_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
      case 14:
        mul_mat_vec_q6_K_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
      case 16:
        mul_mat_vec_iq2_xxs_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
      case 17:
        mul_mat_vec_iq2_xs_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
      case 18:
        mul_mat_vec_iq3_xxs_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
      case 19:
        mul_mat_vec_iq1_s_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
      case 20:
        mul_mat_vec_iq4_nl_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
      case 21:
        mul_mat_vec_iq3_s_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
      case 22:
        mul_mat_vec_iq2_s_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
      case 23:
        mul_mat_vec_iq4_xs_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
      case 29:
        mul_mat_vec_iq1_m_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(), (void*)quant_X.data_ptr(), (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
        break;
    }
  });
  return Y;
}

torch::Tensor ggml_mul_mat_vec_q6_permuted_a8(
    torch::Tensor W,
    torch::Tensor X,
    int64_t row,
    int64_t num_key_heads,
    int64_t values_per_key,
    int64_t head_dim) {
  TORCH_CHECK(X.dim() == 2, "X must be rank 2");
  TORCH_CHECK(X.is_contiguous(), "X must be contiguous");
  const int col = X.sizes()[1];
  const int vecs = X.sizes()[0];
  TORCH_CHECK(
      col == num_key_heads * values_per_key * head_dim,
      "permuted V geometry does not match X width");
  const int padded = (col + 512 - 1) / 512 * 512;
  const at::cuda::OptionalCUDAGuard device_guard(device_of(X));
  auto options = torch::TensorOptions().dtype(X.dtype()).device(W.device());
  at::Tensor Y = torch::empty({vecs, row}, options);
  cudaStream_t stream = at::cuda::getCurrentCUDAStream().stream();
  options = torch::TensorOptions().dtype(torch::kInt32).device(W.device());
  at::Tensor quant_X = torch::empty({vecs, padded / 32 * 9}, options);
  DISPATCH_FLOAT_TYPES(X.scalar_type(), "ggml_mul_mat_vec_q6_permuted_a8", [&] {
    quantize_permuted_v_q8_1_cuda<scalar_t>(
        (scalar_t*)X.data_ptr(), (void*)quant_X.data_ptr(), col, vecs,
        num_key_heads, values_per_key, head_dim, stream);
    mul_mat_vec_q6_K_q8_1_cuda<scalar_t>(
        (void*)W.data_ptr(), (void*)quant_X.data_ptr(),
        (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
  });
  return Y;
}

torch::Tensor ggml_mul_mat_vec_q6_gdn_a8(
    torch::Tensor W,
    torch::Tensor X,
    torch::Tensor Z,
    torch::Tensor norm_weight,
    int64_t row,
    int64_t num_key_heads,
    int64_t values_per_key,
    int64_t head_dim,
    double eps) {
  TORCH_CHECK(X.dim() == 2 && Z.sizes() == X.sizes(), "X/Z shape mismatch");
  TORCH_CHECK(X.is_contiguous() && Z.is_contiguous(), "X/Z must be contiguous");
  TORCH_CHECK(norm_weight.numel() == head_dim, "bad GDN norm weight width");
  const int num_value_heads = num_key_heads * values_per_key;
  TORCH_CHECK(X.sizes()[0] % num_value_heads == 0, "bad GDN row count");
  TORCH_CHECK(X.sizes()[1] == head_dim, "bad GDN head width");
  const int vecs = X.sizes()[0] / num_value_heads;
  const int col = num_value_heads * head_dim;
  const int padded = (col + 512 - 1) / 512 * 512;
  const at::cuda::OptionalCUDAGuard device_guard(device_of(X));
  auto options = torch::TensorOptions().dtype(X.dtype()).device(W.device());
  at::Tensor Y = torch::empty({vecs, row}, options);
  cudaStream_t stream = at::cuda::getCurrentCUDAStream().stream();
  options = torch::TensorOptions().dtype(torch::kInt32).device(W.device());
  at::Tensor quant_X = torch::empty({vecs, padded / 32 * 9}, options);
  DISPATCH_FLOAT_TYPES(X.scalar_type(), "ggml_mul_mat_vec_q6_gdn_a8", [&] {
    const dim3 blocks((col + CUDA_QUANTIZE_BLOCK_SIZE - 1) / CUDA_QUANTIZE_BLOCK_SIZE, vecs, 1);
    const dim3 threads(CUDA_DEQUANTIZE_BLOCK_SIZE, 1, 1);
    quantize_gdn_norm_permuted_v_q8_1<<<blocks, threads, 0, stream>>>(
        (scalar_t*)X.data_ptr(), (scalar_t*)Z.data_ptr(),
        (scalar_t*)norm_weight.data_ptr(), (void*)quant_X.data_ptr(), vecs,
        num_key_heads, values_per_key, head_dim, static_cast<float>(eps));
    mul_mat_vec_q6_K_q8_1_cuda<scalar_t>(
        (void*)W.data_ptr(), (void*)quant_X.data_ptr(),
        (scalar_t*)Y.data_ptr(), col, row, vecs, stream);
  });
  return Y;
}

torch::Tensor ggml_mul_mat_a8(
    torch::Tensor W,  // quant weight
    torch::Tensor X,  // input
    int64_t type,
    int64_t row) {
  int col = X.sizes()[1];
  int padded = (col + 512 - 1) / 512 * 512;
  int batch = X.sizes()[0];
  const at::cuda::OptionalCUDAGuard device_guard(device_of(X));
  auto options = torch::TensorOptions().dtype(X.dtype()).device(W.device());
  at::Tensor Y = torch::empty({batch, row}, options);
  cudaStream_t stream = at::cuda::getCurrentCUDAStream().stream();
  options = torch::TensorOptions().dtype(torch::kInt32).device(W.device());
  at::Tensor quant_X = torch::empty({batch, padded / 32 * 9}, options);
  DISPATCH_FLOAT_TYPES(X.scalar_type(), "ggml_mul_mat_a8", [&] {
    quantize_row_q8_1_cuda((scalar_t*)X.data_ptr(), (void*)quant_X.data_ptr(), col, batch, stream);

    switch (type) {
      case 2:
        ggml_mul_mat_q4_0_q8_1_cuda(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            col,
            row,
            batch,
            padded,
            row,
            stream);
        break;
      case 3:
        ggml_mul_mat_q4_1_q8_1_cuda(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            col,
            row,
            batch,
            padded,
            row,
            stream);
        break;
      case 6:
        ggml_mul_mat_q5_0_q8_1_cuda(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            col,
            row,
            batch,
            padded,
            row,
            stream);
        break;
      case 7:
        ggml_mul_mat_q5_1_q8_1_cuda(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            col,
            row,
            batch,
            padded,
            row,
            stream);
        break;
      case 8:
        ggml_mul_mat_q8_0_q8_1_cuda(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            col,
            row,
            batch,
            padded,
            row,
            stream);
        break;
      case 10:
        ggml_mul_mat_q2_K_q8_1_cuda(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            col,
            row,
            batch,
            padded,
            row,
            stream);
        break;
      case 11:
        ggml_mul_mat_q3_K_q8_1_cuda(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            col,
            row,
            batch,
            padded,
            row,
            stream);
        break;
      case 12:
        ggml_mul_mat_q4_K_q8_1_cuda(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            col,
            row,
            batch,
            padded,
            row,
            stream);
        break;
      case 13:
        ggml_mul_mat_q5_K_q8_1_cuda(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            col,
            row,
            batch,
            padded,
            row,
            stream);
        break;
      case 14:
        ggml_mul_mat_q6_K_q8_1_cuda(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            col,
            row,
            batch,
            padded,
            row,
            stream);
        break;
    }
  });
  return Y;
}

torch::Tensor ggml_moe_a8(
    torch::Tensor X,  // input
    torch::Tensor W,  // expert weights
    torch::Tensor sorted_token_ids,
    torch::Tensor expert_ids,
    torch::Tensor num_tokens_post_padded,
    int64_t type,
    int64_t row,
    int64_t top_k,
    int64_t tokens) {
  int col = X.sizes()[1];
  int padded = (col + 512 - 1) / 512 * 512;
  const at::cuda::OptionalCUDAGuard device_guard(device_of(X));
  auto options = torch::TensorOptions().dtype(X.dtype()).device(W.device());
  at::Tensor Y = torch::empty({tokens * top_k, row}, options);
  cudaStream_t stream = at::cuda::getCurrentCUDAStream().stream();
  options = torch::TensorOptions().dtype(torch::kInt32).device(W.device());
  at::Tensor quant_X = torch::empty({tokens, padded / 32 * 9}, options);
  DISPATCH_FLOAT_TYPES(X.scalar_type(), "ggml_moe_a8", [&] {
    quantize_row_q8_1_cuda((scalar_t*)X.data_ptr(), (void*)quant_X.data_ptr(), col, tokens, stream);
    switch (type) {
      case 2:
        ggml_moe_q4_0_q8_1_cuda(
            (void*)quant_X.data_ptr(),
            (void*)W.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)sorted_token_ids.data_ptr(),
            (int*)expert_ids.data_ptr(),
            (int*)num_tokens_post_padded.data_ptr(),
            W.stride(0),
            col,
            row,
            tokens,
            padded,
            row,
            top_k,
            sorted_token_ids.sizes()[0],
            stream);
        break;
      case 3:
        ggml_moe_q4_1_q8_1_cuda(
            (void*)quant_X.data_ptr(),
            (void*)W.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)sorted_token_ids.data_ptr(),
            (int*)expert_ids.data_ptr(),
            (int*)num_tokens_post_padded.data_ptr(),
            W.stride(0),
            col,
            row,
            tokens,
            padded,
            row,
            top_k,
            sorted_token_ids.sizes()[0],
            stream);
        break;
      case 6:
        ggml_moe_q5_0_q8_1_cuda(
            (void*)quant_X.data_ptr(),
            (void*)W.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)sorted_token_ids.data_ptr(),
            (int*)expert_ids.data_ptr(),
            (int*)num_tokens_post_padded.data_ptr(),
            W.stride(0),
            col,
            row,
            tokens,
            padded,
            row,
            top_k,
            sorted_token_ids.sizes()[0],
            stream);
        break;
      case 7:
        ggml_moe_q5_1_q8_1_cuda(
            (void*)quant_X.data_ptr(),
            (void*)W.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)sorted_token_ids.data_ptr(),
            (int*)expert_ids.data_ptr(),
            (int*)num_tokens_post_padded.data_ptr(),
            W.stride(0),
            col,
            row,
            tokens,
            padded,
            row,
            top_k,
            sorted_token_ids.sizes()[0],
            stream);
        break;
      case 8:
        ggml_moe_q8_0_q8_1_cuda(
            (void*)quant_X.data_ptr(),
            (void*)W.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)sorted_token_ids.data_ptr(),
            (int*)expert_ids.data_ptr(),
            (int*)num_tokens_post_padded.data_ptr(),
            W.stride(0),
            col,
            row,
            tokens,
            padded,
            row,
            top_k,
            sorted_token_ids.sizes()[0],
            stream);
        break;
      case 10:
        ggml_moe_q2_K_q8_1_cuda(
            (void*)quant_X.data_ptr(),
            (void*)W.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)sorted_token_ids.data_ptr(),
            (int*)expert_ids.data_ptr(),
            (int*)num_tokens_post_padded.data_ptr(),
            W.stride(0),
            col,
            row,
            tokens,
            padded,
            row,
            top_k,
            sorted_token_ids.sizes()[0],
            stream);
        break;
      case 11:
        ggml_moe_q3_K_q8_1_cuda(
            (void*)quant_X.data_ptr(),
            (void*)W.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)sorted_token_ids.data_ptr(),
            (int*)expert_ids.data_ptr(),
            (int*)num_tokens_post_padded.data_ptr(),
            W.stride(0),
            col,
            row,
            tokens,
            padded,
            row,
            top_k,
            sorted_token_ids.sizes()[0],
            stream);
        break;
      case 12:
        ggml_moe_q4_K_q8_1_cuda(
            (void*)quant_X.data_ptr(),
            (void*)W.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)sorted_token_ids.data_ptr(),
            (int*)expert_ids.data_ptr(),
            (int*)num_tokens_post_padded.data_ptr(),
            W.stride(0),
            col,
            row,
            tokens,
            padded,
            row,
            top_k,
            sorted_token_ids.sizes()[0],
            stream);
        break;
      case 13:
        ggml_moe_q5_K_q8_1_cuda(
            (void*)quant_X.data_ptr(),
            (void*)W.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)sorted_token_ids.data_ptr(),
            (int*)expert_ids.data_ptr(),
            (int*)num_tokens_post_padded.data_ptr(),
            W.stride(0),
            col,
            row,
            tokens,
            padded,
            row,
            top_k,
            sorted_token_ids.sizes()[0],
            stream);
        break;
      case 14:
        ggml_moe_q6_K_q8_1_cuda(
            (void*)quant_X.data_ptr(),
            (void*)W.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)sorted_token_ids.data_ptr(),
            (int*)expert_ids.data_ptr(),
            (int*)num_tokens_post_padded.data_ptr(),
            W.stride(0),
            col,
            row,
            tokens,
            padded,
            row,
            top_k,
            sorted_token_ids.sizes()[0],
            stream);
        break;
    }
  });
  return Y;
}

torch::Tensor ggml_moe_a8_vec(
    torch::Tensor X,  // input
    torch::Tensor W,  // expert weights
    torch::Tensor topk_ids,
    int64_t top_k,
    int64_t type,
    int64_t row,
    int64_t tokens,
    int64_t expert_stride_bytes) {
  int col = X.sizes()[1];
  const int padded = (col + 512 - 1) / 512 * 512;
  const at::cuda::OptionalCUDAGuard device_guard(device_of(X));
  auto options = torch::TensorOptions().dtype(X.dtype()).device(W.device());
  at::Tensor Y = torch::zeros({tokens * top_k, row}, options);
  cudaStream_t stream = at::cuda::getCurrentCUDAStream().stream();
  options = torch::TensorOptions().dtype(torch::kInt32).device(W.device());
  at::Tensor quant_X = torch::empty({tokens, padded / 32 * 9}, options);
  DISPATCH_FLOAT_TYPES(X.scalar_type(), "ggml_moe_vec_a8", [&] {
    quantize_row_q8_1_cuda<scalar_t>((scalar_t*)X.data_ptr(), (void*)quant_X.data_ptr(), col, tokens, stream);
    switch (type) {
      case 2:
        moe_vec_q4_0_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
      case 3:
        moe_vec_q4_1_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
      case 6:
        moe_vec_q5_0_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
      case 7:
        moe_vec_q5_1_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
      case 8:
        moe_vec_q8_0_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
      case 10:
        moe_vec_q2_K_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
      case 11:
        moe_vec_q3_K_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
      case 12:
        moe_vec_q4_K_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
      case 13:
        moe_vec_q5_K_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
      case 14:
        moe_vec_q6_K_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
      case 16:
        moe_vec_iq2_xxs_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
      case 17:
        moe_vec_iq2_xs_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
      case 18:
        moe_vec_iq3_xxs_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
      case 19:
        moe_vec_iq1_s_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
      case 20:
        moe_vec_iq4_nl_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
      case 21:
        moe_vec_iq3_s_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
      case 22:
        moe_vec_iq2_s_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
      case 23:
        moe_vec_iq4_xs_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
      case 29:
        moe_vec_iq1_m_q8_1_cuda<scalar_t>(
            (void*)W.data_ptr(),
            (void*)quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(),
            (int*)topk_ids.data_ptr(),
            top_k,
            tokens,
            col,
            row,
            quant_X.stride(0),
            expert_stride_bytes,
            stream);
        break;
    }
  });
  return Y;
}

torch::Tensor ggml_moe_shared_a8_vec(
    torch::Tensor X,
    torch::Tensor W,
    torch::Tensor W_shared,
    torch::Tensor routed_ids,
    int64_t routed_top_k,
    int64_t type,
    int64_t row,
    int64_t tokens,
    int64_t expert_stride_bytes,
    bool broadcast) {
  TORCH_CHECK(type == 12 || type == 14, "shared GGUF fusion supports Q4_K/Q6_K");
  TORCH_CHECK(W.is_cuda() && W_shared.is_cuda() && X.is_cuda() && routed_ids.is_cuda());
  TORCH_CHECK(W.dtype() == torch::kUInt8 && W_shared.dtype() == torch::kUInt8);
  TORCH_CHECK(W.dim() == 2 && W_shared.dim() == 2 && W.is_contiguous() && W_shared.is_contiguous());
  TORCH_CHECK(routed_ids.dtype() == torch::kInt32 && routed_ids.is_contiguous());
  TORCH_CHECK(routed_ids.size(0) == tokens && routed_ids.size(1) == routed_top_k);
  TORCH_CHECK(W_shared.size(0) == row, "shared weight row count mismatch");
  const int64_t total_top_k = routed_top_k + 1;
  TORCH_CHECK(X.size(0) == (broadcast ? tokens : tokens * total_top_k), "activation row count mismatch");

  const int col = X.size(1);
  const int padded = (col + 512 - 1) / 512 * 512;
  const at::cuda::OptionalCUDAGuard device_guard(device_of(X));
  auto options = torch::TensorOptions().dtype(X.dtype()).device(W.device());
  at::Tensor Y = torch::zeros({tokens * total_top_k, row}, options);
  cudaStream_t stream = at::cuda::getCurrentCUDAStream().stream();
  options = torch::TensorOptions().dtype(torch::kInt32).device(W.device());
  const int64_t activation_rows = X.size(0);
  at::Tensor quant_X = torch::empty({activation_rows, padded / 32 * 9}, options);
  DISPATCH_FLOAT_TYPES(X.scalar_type(), "ggml_moe_shared_vec_a8", [&] {
    quantize_row_q8_1_cuda<scalar_t>(
        (scalar_t*)X.data_ptr(), (void*)quant_X.data_ptr(), col, activation_rows, stream);
    if (type == 12) {
      moe_vec_with_shared_cuda<scalar_t, QK_K, QI4_K, block_q4_K,
          VDR_Q4_K_Q8_1_MMVQ, vec_dot_q4_K_q8_1>(
          W.data_ptr(), W_shared.data_ptr(), quant_X.data_ptr(),
          (scalar_t*)Y.data_ptr(), (int*)routed_ids.data_ptr(), routed_top_k,
          tokens, col, row, quant_X.stride(0), expert_stride_bytes, broadcast, stream);
    } else {
      moe_vec_with_shared_cuda<scalar_t, QK_K, QI6_K, block_q6_K,
          VDR_Q6_K_Q8_1_MMVQ, vec_dot_q6_K_q8_1>(
          W.data_ptr(), W_shared.data_ptr(), quant_X.data_ptr(),
          (scalar_t*)Y.data_ptr(), (int*)routed_ids.data_ptr(), routed_top_k,
          tokens, col, row, quant_X.stride(0), expert_stride_bytes, broadcast, stream);
    }
  });
  return Y;
}

torch::Tensor ggml_moe_shared_silu_down_a8_vec(
    torch::Tensor GateUp,
    torch::Tensor W,
    torch::Tensor W_shared,
    torch::Tensor routed_ids,
    int64_t routed_top_k,
    int64_t type,
    int64_t row,
    int64_t tokens,
    int64_t expert_stride_bytes,
    bool ada_multiwarp) {
  TORCH_CHECK(type == 12 || type == 14, "shared GGUF fusion supports Q4_K/Q6_K");
  TORCH_CHECK(GateUp.is_cuda() && W.is_cuda() && W_shared.is_cuda() && routed_ids.is_cuda());
  TORCH_CHECK(GateUp.dim() == 2 && GateUp.is_contiguous() && GateUp.size(1) % 2 == 0);
  TORCH_CHECK(W.dtype() == torch::kUInt8 && W_shared.dtype() == torch::kUInt8);
  TORCH_CHECK(W.dim() == 2 && W_shared.dim() == 2 && W.is_contiguous() && W_shared.is_contiguous());
  TORCH_CHECK(routed_ids.dtype() == torch::kInt32 && routed_ids.is_contiguous());
  TORCH_CHECK(routed_ids.size(0) == tokens && routed_ids.size(1) == routed_top_k);
  TORCH_CHECK(W_shared.size(0) == row, "shared weight row count mismatch");
  const int64_t total_top_k = routed_top_k + 1;
  TORCH_CHECK(GateUp.size(0) == tokens * total_top_k, "gate/up route count mismatch");

  const int col = GateUp.size(1) / 2;
  const int padded = (col + 512 - 1) / 512 * 512;
  const int activation_rows = GateUp.size(0);
  const at::cuda::OptionalCUDAGuard device_guard(device_of(GateUp));
  auto options = torch::TensorOptions().dtype(GateUp.dtype()).device(W.device());
  at::Tensor Y = torch::empty({activation_rows, row}, options);
  cudaStream_t stream = at::cuda::getCurrentCUDAStream().stream();
  options = torch::TensorOptions().dtype(torch::kInt32).device(W.device());
  at::Tensor quant_X = torch::empty({activation_rows, padded / 32 * 9}, options);
  DISPATCH_FLOAT_TYPES(GateUp.scalar_type(), "ggml_moe_shared_silu_down_a8_vec", [&] {
    quantize_silu_row_q8_1_cuda<scalar_t>(
        (scalar_t*)GateUp.data_ptr(), quant_X.data_ptr(), col, activation_rows, stream);
    if (type == 12) {
      if (ada_multiwarp) {
        moe_vec_with_shared_cuda<scalar_t, QK_K, QI4_K, block_q4_K,
            VDR_Q4_K_Q8_1_MMVQ, vec_dot_q4_K_q8_1, 4>(
            W.data_ptr(), W_shared.data_ptr(), quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(), (int*)routed_ids.data_ptr(), routed_top_k,
            tokens, col, row, quant_X.stride(0), expert_stride_bytes, false, stream);
      } else {
        moe_vec_with_shared_cuda<scalar_t, QK_K, QI4_K, block_q4_K,
            VDR_Q4_K_Q8_1_MMVQ, vec_dot_q4_K_q8_1>(
            W.data_ptr(), W_shared.data_ptr(), quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(), (int*)routed_ids.data_ptr(), routed_top_k,
            tokens, col, row, quant_X.stride(0), expert_stride_bytes, false, stream);
      }
    } else {
      if (ada_multiwarp) {
        moe_vec_with_shared_cuda<scalar_t, QK_K, QI6_K, block_q6_K,
            VDR_Q6_K_Q8_1_MMVQ, vec_dot_q6_K_q8_1, 4>(
            W.data_ptr(), W_shared.data_ptr(), quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(), (int*)routed_ids.data_ptr(), routed_top_k,
            tokens, col, row, quant_X.stride(0), expert_stride_bytes, false, stream);
      } else {
        moe_vec_with_shared_cuda<scalar_t, QK_K, QI6_K, block_q6_K,
            VDR_Q6_K_Q8_1_MMVQ, vec_dot_q6_K_q8_1>(
            W.data_ptr(), W_shared.data_ptr(), quant_X.data_ptr(),
            (scalar_t*)Y.data_ptr(), (int*)routed_ids.data_ptr(), routed_top_k,
            tokens, col, row, quant_X.stride(0), expert_stride_bytes, false, stream);
      }
    }
  });
  return Y;
}

int64_t ggml_moe_get_block_size(int64_t type) {
  switch (type) {
    case 2:
      return MOE_X_Q4_0;
    case 3:
      return MOE_X_Q4_1;
    case 6:
      return MOE_X_Q5_0;
    case 7:
      return MOE_X_Q5_1;
    case 8:
      return MOE_X_Q8_0;
    case 10:
      return MOE_X_Q2_K;
    case 11:
      return MOE_X_Q3_K;
    case 12:
      return MOE_X_Q4_K;
    case 13:
      return MOE_X_Q5_K;
    case 14:
      return MOE_X_Q6_K;
  }
  return 0;
}

// ---- FreeToken pybind bindings (donor registers these via TORCH_LIBRARY; we
// expose them through torch.utils.cpp_extension.load's pybind module instead) ----
#include <torch/extension.h>
PYBIND11_MODULE(TORCH_EXTENSION_NAME, m) {
  m.def("ggml_dequantize", &ggml_dequantize, "");
  m.def("ggml_mul_mat_vec_a8", &ggml_mul_mat_vec_a8, "");
  m.def("ggml_mul_mat_vec_q6_permuted_a8", &ggml_mul_mat_vec_q6_permuted_a8, "");
  m.def("ggml_mul_mat_vec_q6_gdn_a8", &ggml_mul_mat_vec_q6_gdn_a8, "");
  m.def("ggml_mul_mat_a8", &ggml_mul_mat_a8, "");
  m.def("ggml_moe_a8", &ggml_moe_a8, "");
  m.def("ggml_moe_a8_vec", &ggml_moe_a8_vec, "");
  m.def("ggml_moe_shared_a8_vec", &ggml_moe_shared_a8_vec, "");
  m.def("ggml_moe_shared_silu_down_a8_vec", &ggml_moe_shared_silu_down_a8_vec, "");
  m.def("ggml_moe_get_block_size", &ggml_moe_get_block_size, "");
}
